buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'de.undercouch:gradle-download-task:3.4.3'
  }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'de.undercouch.download'

group = 'edu.mssm.pharm.maayanlab.KEA'
description = 'KEA'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
  mavenCentral()
  maven { url "https://jitpack.io" }
}

dependencies {
  compile 'com.github.MaayanLab.common:common-core:master-SNAPSHOT'
  compile 'com.github.MaayanLab.common:common-math:master-SNAPSHOT'
  compile 'com.github.MaayanLab.common:common-swing:master-SNAPSHOT'
  compile group: 'com.google.code.gson', name: 'gson', version:'2.2.1'
  compile group: 'org.tinyjee.jgraphx', name: 'jgraphx', version:'1.10.1.3'
  testCompile group: 'junit', name: 'junit', version:'3.8.1'
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives sourcesJar
}

task downloadResources(type: Download) {
  src 'http://amp.pharm.mssm.edu/lincs-playground/index.php/s/xq55Gwfyy6zsD7P/download'
  dest new File(buildDir, 'KEA.zip')
  overwrite false
}

task downloadAndUnzipResources(type: Copy) {
  from zipTree(downloadResources.dest)
  into 'src/'
  eachFile { f ->
    f.path = f.path.replaceFirst('KEA/', '')
  }
  includeEmptyDirs false
}

downloadAndUnzipResources.dependsOn downloadResources
processResources.dependsOn downloadAndUnzipResources
